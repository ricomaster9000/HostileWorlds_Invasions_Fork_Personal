buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net/' }
        mavenCentral()
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'

version = "1.0"
group= "com.corosus.inv"
archivesBaseName = "hostileworlds_invasions"

sourceCompatibility = targetCompatibility = '1.8'

ext.configFile = file "build.properties"
configFile.withReader {
    // Load config.  It shall from now be referenced as simply config or project.config
    def prop = new Properties()
    prop.load(it)
    project.ext.config = new ConfigSlurper().parse prop
}
configurations {
    deployerJars
}

version = "${config.minecraft_version}-${config.mod_version}"

minecraft {
    version = "1.12.2-14.23.5.2847"
    mappings = "snapshot_20171003"
}

repositories {
    maven { url = 'https://maven.minecraftforge.net/' }
    maven { url = 'https://cursemaven.com' }
    mavenCentral()
}

dependencies {
    implementation "curse.maven:coroutil-237749:2902920"
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else except the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

jar.finalizedBy('reobfJar')

publishing {
    tasks.publish.dependsOn 'build'
    publications {
        mavenJava(MavenPublication) {
            groupId 'com.corosus'
            artifactId 'hostileworlds_invasions'
            artifact jar
            artifact sourceJar
        }
    }
}
